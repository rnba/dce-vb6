#!/usr/bin/execlineb -WP

multisubstitute {
  importas -i file file
  importas -i t t
  importas -i n n
}
ifelse { test $t = var }
{
  dce-print { "deleting variables does not work yet.  skipping." }
  exit 1
}
multisubstitute {
  define mode.const oneline
  define mode.var  oneline
  define mode.dfun oneline
  define mode.dsub oneline
  define mode.event oneline
  define mode.fun  block
  define mode.sub  block
  define mode.enum block
  define mode.type block
  define mode.pget block
  define mode.plet block
  define mode.pset block

  define kw.const Const
  define kw.var   Dim
  define kw.dfun  Declare
  define kw.dsub  Declare
  define kw.event Event
  define kw.fun   Function
  define kw.sub   Sub
  define kw.enum  Enum
  define kw.type  Type
  define kw.pget  Property
  define kw.plet  Property
  define kw.pset  Property

  define aux.pget Get
  define aux.plet Let
  define aux.pset Set
  define aux.dfun Function
  define aux.dsub Sub

  define beg.const beg.1
  define beg.var   beg.1
  define beg.dfun  beg.2
  define beg.dsub  beg.2
  define beg.event beg.1

  define end.const oneline
  define end.var   oneline
  define end.dfun  oneline
  define end.dsub  oneline
  define end.event oneline

  define beg.fun  beg.1
  define beg.sub  beg.1
  define beg.enum beg.1
  define beg.type beg.1

  define end.fun  end.1
  define end.sub  end.1
  define end.enum end.1
  define end.type end.1

  define beg.pget beg.2
  define beg.plet beg.2
  define beg.pset beg.2

  define end.pget end.1
  define end.plet end.1
  define end.pset end.1
}
multisubstitute {
  define oneline "We don't go to Ravenholm anymore"

  define beg.1 "${kw.${t}}"
  define beg.2 "${kw.${t}}\\s+${aux.${t}}"
  define end.1 "End\\s+${kw.${t}}"
}

define decl "^\\s*((Public|Private)\\s+)?${${beg.${t}}}\\s+${n}\\>"

exec sed -Eni "
  /^'.*/ {
    h; n
    :comment
    /^'.*/ {
      H; n; b comment
    }
    /${decl}/I {
      b ${mode.${t}}
    }
    # the comment did not precede our definition.
    # print the comment saved up in the hold space
    # followed by the current line, then start
    # a new cycle
    x; p
    x; p
    b
  }

  b ${mode.${t}}

  :oneline
  /${decl}/I {
    :lcont
    /_$/ {
      n; b lcont
    }
    n; b tail
  }

  :block
  /${decl}/I {
    :body
    /${${end.${t}}}/I {
      n; b tail
    }
    n; b body
  }

  :unrelated
  p; b

  :tail
  /^\\s*$/ {
    n; b tail;
  }
  b unrelated
  " $file
