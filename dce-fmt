#!/usr/bin/sed -Enf

:start

# collect _ lines in hold space

/(^|\s+)_$/ {
  $ blastline
  s///
  /^$/! {
    x; /\S/ { G; h; }
  }
  n
  s/^\s+//
  b start;
}

s/\s+$//

# current line does not end in "_"
# but maybe the previous one did?
x; /\S/ {
  G; s/\n/ /g;
  # also, we're done with HS, so zap it.
  x; z
}
x

# now let's fix spacing
s/^\s*//

/^((\w+\.)*\w+\$?)/ {
  h; s//<\1>\\n/; w fmt.log
  g; s//\1 \n/
}
:again
s/\n\s*/\n /
/(\w+\$?) \n ([(])/ {
  h; s//[w (]<\1>\\n<\2> /; w fmt.log
  g; s//\1\2 \n/
  b again
}
/([(]) \n ([(])/ {
  h; s//[( (]<\1>\\n<\2> /; w fmt.log
  g; s//\1\2 \n/
  b again
}
/([(]) \n ("[^"]*")/ {
  h; s//[( "]<\1>\\n<\2> /; w fmt.log
  g; s//\1\2 \n/
  b again
}
/([(]) \n (#\w+|((\w+\.)+|\.)?\w+\$?)/ {
  h; s//[( w]<\1>\\n<\2> /; w fmt.log
  g; s//\1\2 \n/
  b again
}
/([^,;]) \n ([,;)])/ {
  h; s//[^,; ,;)]<\1>\\n<\2> /; w fmt.log
  g; s//\1\2 \n/
  b again
}
/(\S+) \n (,)/ {
  h; s//[S ,]<\1>\\n<\2> /; w fmt.log
  g; s//\1 \2 \n/
  b again
}
/(\S+) \n (#\w+|((\w+\.)+|\.)?\w+\$?)/ {
  h; s//[S w]<\1>\\n<\2> /; w fmt.log
  g; s//\1 \2 \n/
  b again
}
/(\S+) \n ([-<>+*/\\=]+)/ {
  h; s//[S %]<\1>\\n<\2> /; w fmt.log
  g; s//\1 \2 \n/
  b again
}
/(\S+) \n ("[^"]*")/ {
  h; s//[S "]<\1>\\n<\2> /; w fmt.log
  g; s//\1 \2 \n/
  b again
}
/(\S+) \n (\S+)/ {
  h; s//[S S]<\1>\\n<\2> /; w fmt.log
  g; s//\1 \2 \n/
  b again
}

/(\S+) (\S+) \n/ {
  h; s//[E]<\1> <\2>\\n /; w fmt.log
  g; s//\1 \2 \n/
}

s/\s*$//
/\n/ {
  h
  s/\n/\\n/
  s/^/NOT HANDLED: /
  w /dev/stderr
  z
  g
}
s/^ //
s/\s+$//

p; z; h

b

:lastline
s/^/ERROR: last line ends with underscore:\n\n/
w /dev/stderr
q 1
