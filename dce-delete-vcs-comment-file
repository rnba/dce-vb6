#!/usr/bin/sed -Ensf

# read a line
#
# if it's a comment:
  # if it has the keywords:
    # drop it,
    # eat the rest of the comment,
    # eat empty lines after it,
    # print the rest of the file
  # else, if it does not:
    # hold it
  # if this is the last line:
    # print it
# else, if it's not a comment:
  # if there's stuff in hold:
  #   print it
  # delete the holdspace
  # print the current line

/^\s*('|Rem\>)/ {
  /\$Workfile:/ {
    n

    :eatcomment
    /^\s*(('|Rem\>).*)$/ {
      n
      b eatcomment
    }

    :eatempties
    /^\s*$/ {
      n
      b eatempties
    }

    :rest
    p; n; b rest
  }
  /\$Workfile:/! {
    H;
  }
  $ p
}

/^\s*('|Rem\>)/! {
  x; /./s/^\n//p;
  z;
  x; p
}

