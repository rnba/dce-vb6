#!/usr/bin/sed -Enf

/^\s*(P(ublic|rivate)\s+)?/ {
  s///
  /^(Function|Sub)\s+([[:alnum:]_]+).*/I {
    s//1 \1 \2/
    b block
  }
  /^(Property)\s+[GS]et\s+([[:alnum:]_]+).*/I {
    s//1 \1 \2/
    b block
  }
  /^(Enum)\s+([[:alnum:]_]+).*/I {
    s//2 \1 \2/
    b block
  }
  /^(Type)\s+([[:alnum:]_]+).*/I {
    s//3 \1 \2/
    b block
  }

  /_$/ {
    s///; h; n
    :cont
    /_$/ {
      s///; H; n; b cont
    }
    H; x
  }

  /^Declare\s+(Function|Sub)\s+([[:alnum:]_]+).*/I {
    s//4 \1 \2/
    p
  }
  /^(Const)\s+([[:alnum:]_]+).*/I {
    s//5 \1 \2/
    p
  }
  #/^([[:alnum:]_]+)[^[:space:]]*\s+As\s.*/I {
  #  s//5 Val \1/
  #  p
  #}

  # it's something else.  next!
  b

  :block
  p
  /^. Function/I {
    :Function
    /^\s*End\s+Function/I! { n; b Function; }
  }
  /^. Sub/I {
    :Sub
    /^\s*End\s+Sub/I! { n; b Sub; }
  }
  /^. Property/I {
    :Property
    /^\s*End\s+Property/I! { n; b Property; }
  }
  /^. Enum/I {
    :Enum
    /^\s*End\s+Enum/I! { n; b Enum; }
  }
  /^. Type/I {
    :Type
    /^\s*End\s+Type/I! { n; b Type; }
  }
}
