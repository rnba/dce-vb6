#!/usr/bin/sed -Ensf

# the "Rem" comment leader is dealt with
# swiftly: grep shows it's only used
# as the first token on a line or inside
# a '-comment.

# lines where either comment leader is
# the first non-WS character are dropped
# completely.

x; z; x

/('|\<Rem\>)/! b asis

/^(\s*)(('|\<Rem\>).*)/ {
  h; s//\1/;
  x; s//\2/
  b print
}

/^([^'"]*\s)('.*)$/ {
  h; s//\1/;
  x; s//\2/
  b print
}

/^(([^'"]*"[^"]*")*[^'"]*)('.*)?$/ {
  h; s//\1/;
  x; s//\3/
  b print
}

:print

x; /\S/! b exit

s/\s+$//

:asis
p

:exit
