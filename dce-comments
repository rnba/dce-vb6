#!/usr/bin/sed -Esf

b main
:lexerr
s/^/dce-comments: bad input: /
w /dev/stderr
Q 1

:asic
# the leader is there and the trailer is empty.
# throw them away
s/^\x1e(.*)\x15$/\1/
# drop the rest added by dce-lex
s/\x1f//g
b

:main
h

# dce-lex starts each line of output with 0x1e
# and puts unrecognizable text behind 0x15
/^\x1e/!  b lexerr
/\x15.+/  b lexerr

# comments get special treatment
/\x1f('.*|Rem\s.*|Rem)\x15/ {
  # drop it (it goes to the end-of-line)
  s//\x15/
  # drop whitespace that preceded it if any
  s/(\x1f\s+)+\x15/\x15/
  # it was all whitespace and comment?
  # eat the newline.
  /^\x1e\x15$/ d
}
