#!/usr/bin/sed -Esf

b main
:lexerr
s/^/dce-comments: bad input: /
w /dev/stderr
Q 1

:main
h

# dce-lex starts each line of output with 0x1e
# and puts unrecognizable text behind 0x15
/^\x1e/!  b lexerr
/\x15.+/  b lexerr

# the leader is there and the trailer is empty.
# throw them away
s/^\x1e(.*)\x15$/\1/

# if any token is either flavor of comment
/\x1f('|Rem(\s|$)).*/ {
  # drop it (it goes to the end-of-line)
  s///
  # drop whitespace that preceded it if any
  s/(\x1f\s+)+$//
  # it was all whitespace and comment?
  # eat the newline.
  /^$/ d
}

s/\x1f//g
